[[naming:Default]]
[role=group,includesConstraints="naming:EntitiesInModelPackages,naming:TestClassName,naming:PackageNameContainsModuleName"]

=== Namensregeln

Die folgende Regeln sind für Namen von Klassen und Packages definiert:

- <<naming:TestClassName>>
- <<naming:EntitiesInModelPackages>>
- <<naming:PackageNameContainsModuleName>>

==== Überprüfung der Testklassennamen

[[naming:TestClassName]]
.Alle JUnit4-Testklassennamen müssen mit `Test` enden.
[source,cypher,role=constraint,requiresConcepts="junit4:TestClass"]
----
MATCH
  (c:Junit4:Test:Class)
WHERE NOT
  c.name =~ ".*Test"
RETURN
  c as InvalidTestClass
----

==== Überprüfung der Klassennamen von Entities

[[naming:EntitiesInModelPackages]]
.Alle JPA Entities müssen in einem Package `entities` sein.
[source,cypher,role=constraint,requiresConcepts="jpa2:Entity"]
----
MATCH
  (p:Package)-[:CONTAINS]->(e:Jpa:Entity)
WHERE
  p.name <> "entities"
RETURN
  e as EntityInWrongPackage
----

==== Überprüfe die Namen der Packages

[[naming:PackageNameContainsModuleName]]
.Alle Java Types müssen in einem Packages enthalten sein, dessen Name mit der Gruppe (`groupId`) und Artefaktnamen (`artifactId`) des Maven-Modules beginnt.
[source,cypher,role=constraint]
----
MATCH
  (:Maven:Project)-[:CREATES]->(a:Artifact)
WITH
  a, REPLACE(REPLACE(a.group + "." + a.name + ".", "-", "."), ".", "\\.") + ".*" as prefixPattern
MATCH
  (a)-[:CONTAINS]->(t:Type)
WHERE NOT
  t.fqn =~ prefixPattern
RETURN
  t as TypeInInvalidPackage
----
